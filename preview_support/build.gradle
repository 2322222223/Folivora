apply plugin: 'java'

def ideInstallationDir = new File('/Applications/Android Studio.app/Contents/')
def androidPluginLibPath = 'plugins/android/lib'

dependencies {
  compileOnly 'com.android.tools:common:26.3.1'
  compileOnly 'com.android.tools.layoutlib:layoutlib-api:26.3.1'
  if (ideInstallationDir.exists() && ideInstallationDir.isDirectory()) {
    ["${androidPluginLibPath}/layoutlib-loader.jar",
     "${androidPluginLibPath}/layoutlib.jar",
     "${androidPluginLibPath}/layoutlib-api.jar",
     "${androidPluginLibPath}/sdklib.jar",
     "${androidPluginLibPath}/lib/android.jar",
     'lib/util.jar',
     'lib/openapi.jar',
     'lib/platform-api.jar'
    ].each { lib ->
      compileOnly files(new File(ideInstallationDir, lib))
    }
  } else {
    throw new GradleException('Invalid ide installation dir')
  }
}

jar {
  archiveName('android-folivora-support.jar')
}

task installToIde(type: Copy) {
  setGroup('install')
  def jarTask = tasks.findByName('jar')
  dependsOn(jarTask)
  from(jarTask.outputs.files)
  into(new File(ideInstallationDir, androidPluginLibPath))
}