apply plugin: 'java'

def ideInstallationDir = new File('/Applications/Android Studio 3.1 Preview.app/Contents/')
def androidPluginLibPath = 'plugins/android/lib'
def lintVersion = '26.2.0-alpha06'

configurations {
  lintChecks
}

dependencies {
  compileOnly "com.android.tools.lint:lint-api:$lintVersion"
  compileOnly "com.android.tools.lint:lint-checks:$lintVersion"
  compileOnly "com.android.tools.lint:lint:$lintVersion"
  compileOnly 'com.android.tools:common:26.3.1'
  compileOnly 'com.android.tools.layoutlib:layoutlib-api:26.3.1'
  if (ideInstallationDir.exists() && ideInstallationDir.isDirectory()) {
    ["${androidPluginLibPath}/layoutlib-loader.jar",
     "${androidPluginLibPath}/layoutlib.jar",
     "${androidPluginLibPath}/layoutlib-api.jar",
     "${androidPluginLibPath}/sdklib.jar",
     "${androidPluginLibPath}/android.jar",
     'lib/util.jar',
     'lib/openapi.jar',
     'lib/platform-api.jar',
     'lib/extensions.jar',
     'lib/idea.jar'
    ].each { lib ->
      compileOnly files(new File(ideInstallationDir, lib))
    }
  } else {
    throw new GradleException('Invalid ide installation dir')
  }
  lintChecks files(jar)
}

jar {
  manifest {
    attributes("Lint-Registry-v2": "cn.cricin.folivora.lint.FolivoraIssueRegistry")
    attributes("Lint-Registry": "cn.cricin.folivora.lint.FolivoraIssueRegistry")
  }
  archiveName('folivora-lint.jar')
}

task installLint(type: Copy) {
  setGroup('install')
  def jarTask = tasks.findByName('jar')
  dependsOn(jarTask)
  from(jarTask.outputs.files)
  into(new File(new File(System.getProperty("user.home")), '.android/lint/'))
}

task uninstallLint() {
  setGroup('install')
  doFirst {
    File lintLibDir = new File(new File(System.getProperty("user.home")), '.android/lint')
    def folivoraLintJar = new File(lintLibDir, 'folivora-lint.jar')
    if (folivoraLintJar.exists()) {
      folivoraLintJar.delete()
    }
  }
}