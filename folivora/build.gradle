apply plugin: 'com.android.library'

def verName = '0.0.6'

android {
  compileSdkVersion 27
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 27
    versionCode 1
    versionName verName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

configurations {
  lintChecks
}

configurations.all {
  transitive false
}

dependencies {
  lintChecks project(path: ':lint', configuration: 'lintChecks')
  compileOnly 'com.android.support:appcompat-v7:27.1.1'
  compileOnly 'androidx.appcompat:appcompat:1.1.0-alpha02'
}

task copyLintJar(type: Copy) {
  from(configurations.lintChecks) {
    rename { 'lint.jar' }
  }
  into 'build/intermediates/lint/'
}

project.afterEvaluate {
  def compileLintTask = project.tasks.find { it.name == 'compileLint' }
  compileLintTask.dependsOn(copyLintJar)
}

ext {
  PUBLISH_GROUP_ID = 'cn.cricin'
  PUBLISH_ARTIFACT_ID = 'folivora'
  PUBLISH_VERSION = verName
}
apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'